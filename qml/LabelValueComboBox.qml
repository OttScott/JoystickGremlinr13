// -*- coding: utf-8; -*-
// SPDX-License-Identifier: GPL-3.0-only

import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Window

import QtQuick.Controls.Universal

import Gremlin.Profile


Item {
    id: _root

    property LabelValueSelectionModel model
    property alias value: _selection.currentValue
    signal selectionChanged()

    implicitHeight: _content.height
    implicitWidth: _content.implicitWidth

    RowLayout {
        id: _content

        anchors.left: parent.left
        anchors.right: parent.right
        spacing: 10

        ComboBox {
            id: _selection

            Layout.minimumWidth: 250
            Layout.fillWidth: true

            model: _root.model
            textRole: "label"
            currentIndex: model ? model.currentSelectionIndex : 0
            delegate: OptionDelegate {}
        }
    }

    // Delegate rendering the selection item using its label but using the
    // associated value for storage
    component OptionDelegate : ItemDelegate {
        required property string label
        required property string value
        required property string bootstrap
        required property string imageIcon

        width: parent.width
        contentItem: Row {
             Label {
                 text: bootstrap

                 width: bootstrap.length > 0 ? 30 : 0
                 verticalAlignment: Text.AlignBottom

                 font.family: "bootstrap-icons"
                 font.pixelSize: 20
             }
             Label {
                text: label
             }
        }

        onClicked: function()
        {
            _root.model.currentValue = value
            selectionChanged()
        }
    }

}

